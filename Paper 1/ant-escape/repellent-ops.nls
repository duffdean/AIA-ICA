; Drop a line of repellent across the south end of the box.
to drop-repellent-pad
  let external-xborder ((max-pxcor - (box-width * (repellent-pad-x / 100))) / 2)
  let external-yborder-lower ((max-pycor - box-height) / 2)
  let external-yborder-upper (external-yborder-lower + (box-height * (repellent-pad-y / 100)))
  
  let x1 0 + external-xborder
  let x2 0 + (max-pxcor - external-xborder)
  let y1 0 + external-yborder-lower + 1
  let y2 0 + external-yborder-upper + 1

  let x x1

  while [x <= x2] [
    let y y1
    
    while [y <= y2] [
      ask patch x y [ drop-repellent ]
      set y y + 1
    ]
    
    set x x + 1
  ]
end

; Drop repellent at the set intensity at a specific patch.
to drop-repellent
  if not is-wall? [
    set repellent repellent-intensity
  ]
end

; Spread the repellent out into all four cardinal directions.
to spread-repellent
  ask patches [
    if old-repellent > 0 [
      distribute-repellent old-repellent
    ]
  ]
end

; Distribute repellent by a specified percentage.
to distribute-repellent [source-repellent]
  let spaces 0
  
  ask neighbors4 [
    if (not is-wall?) and (repellent < source-repellent) [
      set repellent repellent + round (source-repellent * ((repellent-transfer / 4) / 100))
      if repellent > 255 [ set repellent 255 ]
      set spaces spaces + 1
    ]
  ]
  
  set repellent repellent - round (source-repellent * (((repellent-transfer / 4) * spaces) / 100))
  if repellent < 0 [ set repellent 0 ]
end